module(
    name = "monorepo",
    version = "1.0",
)

bazel_dep(name = "bazel_skylib", version = "1.9.0")
bazel_dep(name = "hermetic_cc_toolchain", version = "4.0.1")
bazel_dep(name = "aspect_rules_js", version = "2.1.0")
bazel_dep(name = "bazel_features", version = "1.23.0", repo_name = "proto_bazel_features")
bazel_dep(name = "protobuf", version = "29.0", repo_name = "com_google_protobuf")
bazel_dep(name = "grpc", version = "1.69.0")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_pkg", version = "1.0.1")
bazel_dep(name = "rules_cc", version = "0.0.17")
bazel_dep(name = "rules_python", version = "1.8.0")
bazel_dep(name = "aspect_rules_lint", version = "2.0.0-rc0")
bazel_dep(name = "zlib", version = "1.3.1.bcr.6")
bazel_dep(name = "buildifier_prebuilt", version = "7.3.1")
bazel_dep(name = "rules_go", version = "0.55.0", repo_name = "io_bazel_rules_go")
bazel_dep(name = "gazelle", version = "0.47.0", repo_name = "bazel_gazelle")
bazel_dep(name = "rules_python_gazelle_plugin", version = "1.5.0-rc1")
bazel_dep(name = "rules_foreign_cc", version = "0.12.0")
bazel_dep(name = "rules_apko", version = "1.5.30")
bazel_dep(name = "rules_distroless", version = "0.5.1")
bazel_dep(name = "aspect_rules_swc", version = "2.0.1")
bazel_dep(name = "aspect_rules_ts", version = "3.3.1")
bazel_dep(name = "rules_perl", version = "0.4.1")
bazel_dep(name = "aspect_bazel_lib", version = "2.19.4")
bazel_dep(name = "rules_oci", version = "2.2.6")
bazel_dep(name = "toolchains_llvm", version = "1.4.0")
bazel_dep(name = "rules_shell", version = "0.6.1")

version = use_extension("@proto_bazel_features//private:extensions.bzl", "version_extension")
use_repo(version, "bazel_features_globals", "bazel_features_version")

# NodeJS version is determined by rules_nodejs default
bazel_dep(name = "rules_nodejs", version = "6.3.0")

archive_override(
    module_name = "aspect_rules_js",
    integrity = "sha256-dcJaDxWp5FkrvaRbV6oInkvxf5F2/XNTUejGRE34e1I=",
    patch_strip = 1,
    patches = ["//bazel/patches:rules_js.patch"],
    strip_prefix = "rules_js-2.1.0",
    urls = ["https://github.com/aspect-build/rules_js/releases/download/v2.1.0/rules_js-v2.1.0.tar.gz"],
)

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm", dev_dependency = True)
npm.npm_translate_lock(
    name = "npm",
    # Disable lifecycle hooks for hermeticity.
    lifecycle_hooks = {
        "*": [],
    },
    # This is important to prevent downloading prebuilt binaries for every platform
    # *cough cough* swc/esbuild/etc.
    # no_optional = True,
    npmrc = "//:.npmrc",
    patch_args = {"*": ["-p1"]},
    pnpm_lock = "//:pnpm-lock.yaml",
    quiet = False,
    use_home_npmrc = False,
    verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(npm, "npm")

pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm")

# Allows developers to use the matching pnpm version, for example:
# bazel run -- @pnpm --dir /home/runner/work/rules_js/rules_js install
use_repo(pnpm, "pnpm")

### Rules TS
rules_ts_ext = use_extension("@aspect_rules_ts//ts:extensions.bzl", "ext", dev_dependency = True)
rules_ts_ext.deps(
    ts_version_from = "//:package.json",
)
use_repo(rules_ts_ext, "npm_typescript")

### Hermetic CC Toolchain
zig_toolchains = use_extension("@hermetic_cc_toolchain//toolchain:ext.bzl", "toolchains")
use_repo(zig_toolchains, "zig_sdk")

register_toolchains(
    "@zig_sdk//toolchain:linux_amd64_gnu.2.28",
    "@zig_sdk//toolchain:linux_arm64_gnu.2.28",
    "@zig_sdk//toolchain:darwin_amd64",
    "@zig_sdk//toolchain:windows_amd64",
    "@zig_sdk//toolchain:windows_arm64",
)

# MacOS ARM64 only; others use Zig hermetic
# Based on the issue -- https://github.com/uber/hermetic_cc_toolchain/issues/10
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    llvm_versions = {
        "darwin-aarch64": "15.0.0",
    },
)
use_repo(llvm, "llvm_toolchain")

# rules_go
go_sdk = use_extension("@io_bazel_rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(version = "1.24.0")

go_deps = use_extension("@bazel_gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
use_repo(go_deps, "com_github_docker_docker", "com_github_google_go_github_v38", "com_github_spf13_cobra", "com_github_stretchr_testify", "org_golang_x_oauth2")

### Rules Apko
apko = use_extension("@rules_apko//apko:extensions.bzl", "apko")
apko.translate_lock(
    name = "apko_wolfi_base",
    lock = "base_images/apko/wolfi-base/apko.lock.json",
)
use_repo(apko, "apko_wolfi_base")

### Rules Distroless
apt = use_extension("@rules_distroless//apt:extensions.bzl", "apt")
apt.install(
    name = "debian12",
    lock = "//base_images/debian:debian12.lock.json",
    manifest = "//base_images/debian:debian12.yaml",
)
use_repo(apt, "debian12")

### Rules OCI
oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "distroless_base",
    digest = "sha256:ccaef5ee2f1850270d453fdf700a5392534f8d1a8ca2acda391fbb6a06b81c86",
    image = "gcr.io/distroless/base",
    platforms = [
        "linux/amd64",
        "linux/arm64",
    ],
)
oci.pull(
    name = "ubuntu22",
    digest = "sha256:b492494d8e0113c4ad3fe4528a4b5ff89faa5331f7d52c5c138196f69ce176a6",
    image = "index.docker.io/library/ubuntu",
)
oci.pull(
    name = "build",
    digest = "sha256:2a669a950ee7941bf0e308372aa9ab1a38dc5491f75e3d6acf7b5e941a3bc32f",
    image = "index.docker.io/juanzolotoochin/ubuntu-build",
)
oci.pull(
    name = "postgres",
    digest = "sha256:cd116dbc6ac97b18f8e1ad07aa94399117863731ec99f06ae1270937a98119bf",
    image = "index.docker.io/library/postgres",
)
oci.pull(
    name = "ubuntu",
    digest = "sha256:eea2c875bb135db3a5d5c959a4161eecce3f6a988cf054125d7f4e836e93e020",
    image = "index.docker.io/juanzolotoochin/ubuntu-base",
)
use_repo(oci, "build", "distroless_base", "distroless_base_linux_amd64", "distroless_base_linux_arm64", "postgres", "ubuntu", "ubuntu22")

# rules_python
python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    python_version = "3.11",
)
use_repo(python, "python_3_11")

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "monorepo_deps",
    python_version = "3.11",
    requirements_lock = "//:requirements_lock.txt",
)
use_repo(pip, pip_deps = "monorepo_deps")

# Http Archives
http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "libssh2",
    build_file = "//third_party/libssh2:BUILD.libssh2.bazel",
    sha256 = "d5fb8bd563305fd1074dda90bd053fb2d29fc4bce048d182f96eaa466dfadafd",
    strip_prefix = "libssh2-1.9.0",
    type = "tar.gz",
    urls = [
        "https://mirror.bazel.build/github.com/libssh2/libssh2/releases/download/libssh2-1.9.0/libssh2-1.9.0.tar.gz",
        "https://github.com/libssh2/libssh2/releases/download/libssh2-1.9.0/libssh2-1.9.0.tar.gz",
    ],
)

http_archive(
    name = "pcre",
    build_file = "//third_party/pcre:BUILD.pcre.bazel",
    sha256 = "04e214c0c40a97b8a5c2b4ae88a3aa8a93e6f2e45c6b3534ddac351f26548577",
    strip_prefix = "pcre2-10.37",
    urls = [
        "https://github.com/PCRE2Project/pcre2/releases/download/pcre2-10.37/pcre2-10.37.tar.gz",
    ],
)

http_archive(
    name = "openssl",
    build_file = "//third_party/openssl:BUILD.openssl3.bazel",
    sha256 = "aa7d8d9bef71ad6525c55ba11e5f4397889ce49c2c9349dcea6d3e4f0b024a7a",
    strip_prefix = "openssl-3.0.5",
    urls = ["https://www.openssl.org/source/openssl-3.0.5.tar.gz"],
)

http_archive(
    name = "alsa",
    build_file = "//third_party/alsa:BUILD.alsa.bazel",
    sha256 = "4868cd908627279da5a634f468701625be8cc251d84262c7e5b6a218391ad0d2",
    strip_prefix = "alsa-lib-1.2.12",
    urls = [
        "https://www.alsa-project.org/files/pub/lib/alsa-lib-1.2.12.tar.bz2",
    ],
)

http_archive(
    name = "docker",
    build_file = "//third_party/binaries:BUILD.docker.bazel",
    sha256 = "23db817b99aac6d5d7fcb1f706e50b5a412d78a9438975d6b4a54c58dc409bfb",
    strip_prefix = "docker",
    url = "https://download.docker.com/linux/static/stable/x86_64/docker-25.0.4.tgz",
)

bazel_dep(name = "rules_rust", version = "0.56.0")

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(edition = "2021")
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
crate.spec(
    package = "ferris-says",
    version = "0.3.1",
)
crate.from_specs()
use_repo(crate, "crates")

CHROME_BUILD_CONTENT = """
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
package(default_visibility = ["//visibility:public"])
filegroup(
    name = "all_files",
    srcs = glob(["**/*"]),
)
native_binary(
    name = "chromium-bin",
    src = "{binary}",
    out = "{binary}",
    data = [":all_files"],
)
"""

http_archive(
    name = "chromium_linux_amd64",
    build_file_content = CHROME_BUILD_CONTENT.format(binary = "chrome-linux/chrome"),
    sha256 = "e6091f35f81cfbdf4fd3eb4f3f7916e841d43582f113f39f6e0d0c253485e56a",
    url = "https://playwright.azureedge.net/builds/chromium/1129/chromium-linux.zip",
)

http_archive(
    name = "chromium_linux_arm64",
    build_file_content = CHROME_BUILD_CONTENT.format(binary = "chrome-linux/chrome"),
    sha256 = "67aab2cec5a4927667575452a2b6c57566085c154f47f924d6e3efa47f7bd777",
    url = "https://playwright.azureedge.net/builds/chromium/1129/chromium-linux-arm64.zip",
)

http_archive(
    name = "chromium_macos_arm64",
    build_file_content = CHROME_BUILD_CONTENT.format(binary = "chrome-mac/Chromium.app/Contents/MacOS/Chromium"),
    sha256 = "59d179d0ad9ad792e51db81aef2d7bcc164e6f5df434c081888fbbeb4568bd0e",
    url = "https://playwright.azureedge.net/builds/chromium/1129/chromium-mac-arm64.zip",
)
